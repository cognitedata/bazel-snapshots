load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")

go_library(
    name = "snapshots_lib",
    srcs = [
        "collect.go",
        "config.go",
        "diff.go",
        "digest.go",
        "get.go",
        "push.go",
        "snapshots.go",
        "tag.go",
        "utils.go",
    ],
    importpath = "github.com/cognitedata/bazel-snapshots/snapshots/go/cmd/snapshots",
    visibility = ["//visibility:public"],
    deps = [
        "//snapshots/go/pkg/bazel",
        "//snapshots/go/pkg/config",
        "//snapshots/go/pkg/models",
        "//snapshots/go/pkg/storage",
        "@com_github_olekukonko_tablewriter//:tablewriter",
        "@com_github_spf13_pflag//:pflag",
        "@io_beyondstorage_go_v5//types",
        "@org_golang_google_grpc//:grpc",
    ],
)

go_binary(
    name = "snapshots",
    embed = [":snapshots_lib"],
    visibility = ["//visibility:public"],
)

PLATFORMS = [
    ("linux", "amd64"),
    ("linux", "arm64"),
    ("darwin", "amd64"),
    ("darwin", "arm64"),
]

[
    go_binary(
        name = "snapshots-{goos}-{goarch}".format(
            goarch = goarch,
            goos = goos,
        ),
        embed = [":snapshots_lib"],
        goarch = goarch,
        goos = goos,
        visibility = ["//visibility:public"],
    )
    for (goos, goarch) in PLATFORMS
]
